# CWB100 (Request and Accept Measurements from CWB100)

The CWB100 instruction is used to set up the datalogger to request and accept measurements from the CWB100 wireless sensor base.

## Syntax

CWB100(CWBPort,CWSDest,CWSConfig)

In the example program below, a 109 probe attached to a CWS900 and a CS655 are measured in a wireless network. No configuration string is used in the CWB100 instruction; thus, the two sensors will be added to the CWSArray as they are found, and default variable names will be used.

Additional configuration strings are given as comments in the program, to provide an example of the different ways in which a configuration string can be used to control the names of the variables stored by the datalogger.

Note that ArrayIndex is also used in two of the data tables to return data values of measurements whose indices are unknown at the time of programming.

Every four hours the CWB100Routes and CWB100RSSI instructions are run to return the routes of the sensors in the network and the radio signal strength of each sensor. Prior to the execution of the CWB100RSSI instruction, the values for RSSI in both sensors will be reflected asNAN

**Note:** Not a number. A data word indicating a measurement or processing error. Voltage overrange, SDI-12 sensor error, and undefined mathematical results can produce NAN.

.

```
Const NumValues = 12
Public CWSArray(NumValues), WirelessRoutes As String * 50
Public ConfigString As String * 125 = "12530A40138D CWS109 4 Temp IntTemp Batt Signal,12530A4013A7 CWS655 8 VH20 EC Temp Period AR IntTemp Batt Signal"
Public PTemp, BattV

DataTable (WSN5min,True,1000)
DataInterval (0,5,min,10)
Sample (NumValues,CWSArray(),FP2)
EndTable

DataTable (WSN60min,True,1000)
DataInterval (0,1,Hr,10)
Average (1,PTemp,FP2,False)
Minimum (1,BattV,FP2,False,False)
Average (1,CWSArray(ArrayIndex("CWS900_1_Ts")),FP2,False)
FieldNames ("CWS900_Temp")
Average (3,CWSArray(ArrayIndex("CWS655_1_VWC")),FP2,False)
FieldNames ("CWS655_VWC")
EndTable

DataTable (Diag,True,60)
Sample (1,WirelessRoutes,String)
Sample (1,CWSArray(ArrayIndex("CWS900_1_SS")),FP2)
FieldNames ("CWS900_Signal")
Sample (1,CWSArray(ArrayIndex("CWS655__1_SS")),FP2)
FieldNames ("CWS655_Signal")
EndTable

BeginProg
Scan (5,min,3,0)
PanelTemp (PTemp,250)
Battery (BattV)

'No Config string, network autodiscovery
CWB100(C1,CWSArray())

'>>>>>Other example config strings<<<<<
'Config string using file stored on CP
'CWB100 (C1,CWSArray(),"CPU:CWSConfig.txt")

'Config string for 109 probe and CWS655, all values w/unique names
'CWB100 (C1,CWSArray(),"12530A40138D CWS109 4 Temp IntTemp Batt Signal,12530A4013A7 CWS655 8 VH20 EC Temp Period AR IntTemp Batt Signal")
'Config string using variable for string
'CWB100 (C1,CWSArray(),ConfigString)

'Config string for 109 probe and CWS655, 2 values and 6 values (no BV or SS) w/unique names
'CWB100 (C1,CWSArray(),"12530A40138D CWS109 2 Temp IntTemp,12530A4013A7 CWS655 6 VH20 EC Temp Period AR IntTemp")
'Every four hours, return network routing and get sensor strength
If IfTime (0,4,Hr) = True Then
CWB100Routes(C1,WirelessRoutes)
CWB100RSSI(C1)
CallTable (Diag)
EndIf

CallTable (WSN5min)
CallTable (WSN60min)
NextScan

EndProg
```

## Remarks

Each time the CWB100 instruction is executed, the wireless sensor base polls the network of wireless sensors for data. The fastest polling rate that can be achieved in a wireless sensor network is determined by the number of sensors in the network, along with the number of hops to each sensor (a hop is a sensor's link through a routing sensor back to the wireless base). At least 15 seconds is required to poll each sensor and retrieve its data. Each hop adds 15 seconds. Thus, the rate at which the CWB100 instruction is executed should be no faster than 15 seconds per sensor, per hop. If the polling rate of the sensor network is too fast, sensor values will alternate betweenNAN

**Note:** Not a number. A data word indicating a measurement or processing error. Voltage overrange, SDI-12 sensor error, and undefined mathematical results can produce NAN.

and a good value with each scan.

The [CWB100Routes](cwb100routes2.md) instruction can be used to determine the route and number of hops to each sensor in the network.

In addition to polling for data, the wireless base will report back to the datalogger any new sensors that are discovered in the network. Newly added sensors may take 10 minutes or longer to be discovered. This auto-discovery of sensors allows new sensors to be added to the network without reprogramming the datalogger. The programmer just needs to consider future requirements for sensors and ensure the CWSDest array is dimensioned large enough to accommodate any new sensors added.

Note that the CWB100 instruction is a processing instruction, not a measurement instruction. Thus, the data from this instruction can lag behind other measurements (since measurements take priority over processing in the datalogger). If the wireless sensor data you are storing to a table needs to be synchronized with other measured sensors, the CWB100 instruction and the CallTable can be placed within an IfTime statement. The program should be run in PipelineMode and the BufferSize in the Scan instruction set large enough to hold the measurement data being stored while the CWB100 instruction is being executed.

## Parameters

# CWBPort (CWB Terminal)

The CWBPort parameter is used to specify the terminal pair on the datalogger to which the wireless sensor base s Data/A terminal will be connected. Valid options are C1, C3, U1, U3, U5, U7, U9, or U11.

Right-click the parameter to display a drop-down list of valid options.

Type: Constant

# CWSDest (Destination Variable)

A variable array that will hold the sensor values returned by the base. CWSDest should be dimensioned large enough to accommodate all sensor values that will be returned. If CWSDest is not dimensioned large enough to hold all values from a sensor, then no values will be reflected for that sensor and it will appear as if the sensor has not been discovered (i.e., the last few elements of the array will remain at the name of the CWSDestArray rather than changing to reflect the name of the new sensor).

If the base is not properly connected to the datalogger as specified by the CWB100 instruction, a -1 will be returned in the first element of this array and the remaining elements will beNAN

**Note:** Not a number. A data word indicating a measurement or processing error. Voltage overrange, SDI-12 sensor error, and undefined mathematical results can produce NAN.

. If a device other than the CWB100 is connected to the control port, a -2 will be returned. If the signature of the field names held by the CWB100 does not match that held by the datalogger, a -3 will be returned (though this error will be corrected on the next scan).

All sensors return at least 4 values: at least one measurement value and three status values (internal temperature Ti, battery voltage BV, and RF signal strength SS). Signal strength will remain NAN until the [CWB100RSSI](cwb100rssi2.md) instruction is executed. Refer to the wireless sensor manual for complete information on what values are returned by each sensor.

Type: Variable array

# CWSConfig (String or Path to Sensor Information)

The use of the CWSConfig parameter is optional and can be left blank. This parameter is a string or the path to a file that contains the sensor name and variable names that the datalogger will use for each sensor. The string or file also determines the order in which each sensor's data will be stored in the datalogger.

CWSConfig string The configuration string contains a sensor ID and sensor name for each sensor, the number of values to include for each sensor, and the fieldnames (in the order they are returned by the sensor) to be used by the datalogger (i.e., Public variables). Each part of the string for a sensor is separated by a space. As an example, a string for one sensor would contain:

SensorID SensorName NumberOfValues Field1Name Field2Name Field3Name

Strings for multiple sensors are separated by a comma. If a sensor returns 10 values, but 4 is specified for the NumberOfValues, then only 4 values will be stored by the datalogger for that sensor.

CWSConfig filename Rather than entering a string for the CWSConfig parameter, a filename can be entered. It is specified as Device:CWSConfig.txt, where Device is CPU, CRD, USB, or USR. If the Network Planner or Wireless Sensor Planner is used to configure the wireless sensors, this file will be created when the "Send a config string to the datalogger" task is chosen (or it can be created manually by the user).

Any sensors in the network not contained in the CWSConfig string will assume their default names when discovered in the network.

Type: Constant String (SensorID SensorName NumberOfValues Field1Name Field2Name )
